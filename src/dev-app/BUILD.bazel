load("@build_bazel_rules_nodejs//:index.bzl", "pkg_web")
load("//tools:defaults.bzl", "ng_module")
load("//tools/dev-server:index.bzl", "dev_server")
load("//tools/esbuild:index.bzl", "esbuild", "esbuild_config")
load("//src/ajf-examples:config.bzl", "ALL_EXAMPLES")
load("//tools/angular:index.bzl", "LINKER_PROCESSED_FW_PACKAGES")

package(default_visibility = ["//visibility:public"])

ng_module(
    name = "dev-app",
    srcs = [
        "dev-app.ts",
        "main.ts",
        "main-module.ts",
        "routes.ts",
    ],
    deps = [
        "//src/dev-app/dev-app",
        "//src/dev-app/example",
        "//src/dev-app/examples-page",
        "//src/dev-app/file-input",
        "//src/dev-app/i18n",
        "//src/dev-app/ion-barcode",
        "//src/dev-app/ion-calendar",
        "//src/dev-app/ion-checkbox-group",
        "//src/dev-app/ion-fields",
        "//src/dev-app/ion-forms",
        "//src/dev-app/ion-image",
        "//src/dev-app/ion-node-icon",
        "//src/dev-app/ion-page-slider",
        "//src/dev-app/ion-reports",
        "//src/dev-app/ion-widgets",
        "//src/dev-app/mat-barcode",
        "//src/dev-app/mat-calendar",
        "//src/dev-app/mat-calendar-ethiopian",
        "//src/dev-app/mat-checkbox-group",
        "//src/dev-app/mat-fields",
        "//src/dev-app/mat-form-builder",
        "//src/dev-app/mat-forms",
        "//src/dev-app/mat-image",
        "//src/dev-app/mat-node-icon",
        "//src/dev-app/mat-page-slider",
        "//src/dev-app/mat-report-builder",
        "//src/dev-app/mat-report-from-form",
        "//src/dev-app/mat-reports",
        "//src/dev-app/mat-time",
        "//src/dev-app/mat-widgets",
        "@npm//@angular/cdk",
        "@npm//@angular/router",
        "@npm//@gic/angular",
        "@npm//@ionic/angular",
    ],
)

esbuild_config(
    name = "esbuild_config",
    config_file = "esbuild.config.mjs",
    deps = [
        "//tools/esbuild:custom_resolve_esbuild_plugin",
    ],
)

esbuild(
    name = "bundles",
    config = ":esbuild_config",
    entry_points = [":main.ts"] + ["%s:index.ts" % e for e in ALL_EXAMPLES],
    platform = "browser",
    splitting = True,
    # We cannot use `ES2017` or higher as that would result in `async/await` not being downleveled.
    # ZoneJS needs to be able to intercept these as otherwise change detection would not work properly.
    target = "es2016",
    # Note: We add all linker-processed FW packages as dependencies here so that ESBuild will
    # map all framework packages to their linker-processed bundles from `tools/angular`.
    deps = LINKER_PROCESSED_FW_PACKAGES + [
        ":dev-app",
    ],
)

# File group for all static files which are needed to serve the dev-app. These files are
# used in the devserver as runfiles and will be copied into the static web package that can
# be deployed on static hosting services (like firebase).
filegroup(
    name = "dev_app_static_files",
    srcs = [
        "favicon.ico",
        "index.html",
        "@npm//:node_modules/@ajf/icons/css/ajf-icons.css",
        "@npm//:node_modules/@ajf/icons/fonts/ajf.eot",
        "@npm//:node_modules/@ajf/icons/fonts/ajf.svg",
        "@npm//:node_modules/@ajf/icons/fonts/ajf.ttf",
        "@npm//:node_modules/@ajf/icons/fonts/ajf.woff",
        "@npm//:node_modules/@angular/material/prebuilt-themes/indigo-pink.css",
        "@npm//:node_modules/core-js-bundle/index.js",
        "@npm//:node_modules/flag-icon-css/css/flag-icon.min.css",
        "@npm//:node_modules/flag-icon-css/flags/4x3/bf.svg",
        "@npm//:node_modules/leaflet/dist/leaflet.css",
        "@npm//:node_modules/rxjs/bundles/rxjs.umd.min.js",
        "@npm//:node_modules/zone.js/dist/zone.js",
    ],
)

dev_server(
    name = "devserver",
    srcs = [":dev_app_static_files"],
    additional_root_paths = [
        "npm/node_modules",
        # Needed for compatibility with "pkg_web" which always uses the tree
        # artifact output as workspace root.
        "gc_ajf",
    ],
    tags = ["manual"],
    deps = [
        ":bundles",
    ],
)

# Target that builds a static web package of the dev-app. The web package can be
# deployed on static hosting services (such as firebase).
pkg_web(
    name = "web_package",
    srcs = [
        ":bundles",
        ":dev_app_static_files",
    ],
    additional_root_paths = [
        "external/npm",
        "npm/node_modules",
    ],
    tags = ["manual"],
)
